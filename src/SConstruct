import os
from os import path

pylaunchyFiles = Split("""
    catalog.cpp plugin_interface.cpp 
    PyLaunchyPlugin.cpp NullScriptPlugin.cpp
    ScriptPluginWrapper.cpp
    python_export.cpp python_export_pylaunchy.cpp
    python_export_qstring.cpp python_export_catalog.cpp
    python_export_ScriptPlugin.cpp
""")
pylaunchySources = []
for file in pylaunchyFiles:
    pylaunchySources.append("%s/%s" % ("pylaunchy", file))


qtIncludePath = path.join( os.environ['QTDIR'], 'include')
qt4ToolLocation = '.'

includePath = [ 
    qtIncludePath,
    path.join( qtIncludePath, 'QtCore' ),
    path.join( qtIncludePath, 'QtGui' ),
    path.join( os.environ['PYTHON_ROOT'], 'include'),
    os.environ['BOOST_1_35_0'],
    'ezlogger',
    'pylaunchy',
    
    os.environ['INCLUDE'].split(';'), # For Visual Studio Express
]
libPath = [
    path.join( os.environ['PYTHON_ROOT'], 'libs'),
    path.join( os.environ['BOOST_1_35_0'], 'lib'),
    
    os.environ['LIB'].split(';'), # For Visual Studio Express
]

flags = Split("""/nologo /Zm200 /Zc:wchar_t- /O2 /MD /GR /EHsc /W3 /w34100 /w34189 /FI Precompiled.h""")

env = Environment(
    CPPPATH=includePath, CCFLAGS=flags,
    tools=['default', 'qt4'],
    toolpath=['.']
)

env.EnableQt4Modules(["QtGui","QtCore","QtNetwork"], debug=False)


env['PCHSTOP'] = 'Precompiled.h'
#env['PCH'] = 'Precompiled.pch'
env['PCH'] =  env.PCH('pylaunchy/Precompiled.cpp')[0]

env.Append(CCFLAGS = ['-D_CRT_SECURE_NO_DEPRECATE'])
env.Append(CPPPATH = includePath)
env.Append(LIBPATH = libPath)
env.Append(LIBS = 'python25')
env.Append(LIBS = 'shell32')

pylaunchyDll = env.SharedLibrary(
    target = 'PyLaunchy', 
    source = pylaunchySources   
)

# Add a post-build step to embed the manifest using mt.exe
# The number at the end of the line indicates the file type (1: EXE; 2:DLL).
env.AddPostAction(pylaunchyDll, 'mt.exe -nologo -manifest ${TARGET}.manifest -outputresource:$TARGET;2')

env.Program(
    target = 'PyLaunchyTester',
    source = ['pylaunchy_tester/pylaunchy_tester.cpp']
)